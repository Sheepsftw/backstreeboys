class PlayerInput : ZilchComponent
{
    //which gamepad it's gonna detect
    [Property]
    var PlayerIndex : Integer;
    //declare the controller
    var Controller : Gamepad;
    
    [Property]
    var MoveSprite : SpriteSource = null;
    
    [Property]
    var StillSprite : SpriteSource = null;
    
    [Property]
    var StunSprite : SpriteSource = null;
    
    var Counter : Integer = 0;
    
    var Valid : Boolean = true;
    
    var StunTimer : Integer = 0;
    function Initialize(init : CogInitializer)
    {
        
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
       
    }
    //jump mechanic
    function OnJumpPressed(event : GamepadEvent)
    {
        if(this.Valid)
        {
            if(event.Button == Buttons.A)
            {
                this.Owner.SweptController.Jump();
            }
        }
    }
    function OnPunchPressed(event : GamepadEvent)
    {
        if(this.Valid)
        {
            if(event.Button == Buttons.X)
            {
                var createloc = this.Owner.Transform.Translation;
                if(this.Owner.Sprite.FlipX)
                {
                    createloc.X -= 1.3;
                }
                else
                {
                    createloc.X += 1.3;
                }
                var thisPunch = this.Space.CreateAtPosition(Archetype.Find("Punch"), createloc);
                thisPunch.PunchScripte.Player = this.Owner;
                if(this.StillSprite.Name == "mowglistill")
                {
                    thisPunch.Sprite.SpriteSource = SpriteSource.Find("armlilm");
                }
                else if(this.StillSprite.Name == "queenbstill")
                {
                    thisPunch.Sprite.SpriteSource = SpriteSource.Find("armyonce");
                }
                else
                {
                    thisPunch.Sprite.SpriteSource = SpriteSource.Find("armspruce");
                }
                if(this.Owner.Sprite.FlipX)
                {
                    thisPunch.Sprite.FlipX = true;
                }
            }
        }
    }
    
    function OnLogicUpdate(event : UpdateEvent)
    {
        if(this.Counter == 2)
        {
            this.Controller = Zero.Gamepads.GetGamePad(this.PlayerIndex);
            Zero.Connect(this.Controller, Events.ButtonDown, this.OnJumpPressed);
            Zero.Connect(this.Controller, Events.ButtonDown, this.OnPunchPressed);
        }
        ++this.Counter;
        if(this.Counter > 2)
        {
            var movement = Real3();
            if(this.Valid)
            {
                //which direction
                var horzmovement = this.Controller.LeftStick.X;
                movement.X += horzmovement;
                //flipping the sprite to face the direction
                if(horzmovement > 0)
                {
                    this.Owner.Sprite.FlipX = false;
                    //changing the Collider to be realistic
                    var newBoxCollider = Real3();
                    newBoxCollider.X = -0.3;
                    newBoxCollider.Y = 0.1;
                    this.Owner.BoxCollider.Offset = newBoxCollider;
                    if(this.Owner.Sprite.SpriteSource != this.MoveSprite)
                    {
                        this.Owner.Sprite.SpriteSource = this.MoveSprite;
                    }
                }
                else if(horzmovement < 0)
                {
                    this.Owner.Sprite.FlipX = true;
                    //see above
                    var newBoxCollider = Real3();
                    newBoxCollider.X = 0.3;
                    newBoxCollider.Y = 0.1;
                    this.Owner.BoxCollider.Offset = newBoxCollider;
                    if(this.Owner.Sprite.SpriteSource != this.MoveSprite)
                    {
                        this.Owner.Sprite.SpriteSource = this.MoveSprite;
                    }
                }
                else
                {
                    this.Owner.Sprite.SpriteSource = this.StillSprite;
                }
            }
            else
            {
                if(this.StunTimer == 0)
                {
                    this.Controller.Vibrate(1, 1, 1);
                }
                if(this.Owner.Sprite.SpriteSource != this.StunSprite)
                {
                    this.Owner.Sprite.SpriteSource = this.StunSprite;
                }
                if(this.StunTimer == 100)
                {
                    this.Valid = true;
                    this.StunTimer = 0;
                }
                else
                {
                    ++this.StunTimer;
                }
            }
            this.Owner.SweptController.Update(movement, event.Dt);
        }
    }
}
